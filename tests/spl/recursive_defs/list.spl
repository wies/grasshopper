options "-abspreds"

include "../include/sllist_rec.spl"

predicate lseg_grass(x: Node, y: Node) {
  acc({ z: Node :: Btwn(next, x, z, y) && z != y }) &*& Reach(next, x, y)
}

lemma lseg_to_lseg_grass(x: Node, y: Node)
  requires lseg(x, y)
  ensures lseg_grass(x, y)
{
  if (x == y) {
  } else {
    lseg_to_lseg_grass(x.next, y);
  }
}

lemma lseg_grass_to_lseg(x: Node, y: Node)
  requires lseg_grass(x, y)
  ensures lseg(x, y)
{
  if (x == y) {
  } else {
    lseg_grass_to_lseg(x.next, y);
  }
}

lemma unfold(x: Node, y: Node)
  requires lseg(x, y) &*& x != y
  ensures acc(x) &*& lseg(x.next, y)
{
}

lemma fold_left(x: Node, y: Node)
  requires acc(x) &*& lseg(x.next, y) &*& y != x
  ensures lseg(x, y)
{
}

lemma compose(x: Node, y: Node, z: Node)
  requires exists X: Set<Node> :: (lseg(x, y) &*& lseg(y, z)) && acc(X) &*& z !in X
  ensures lseg(x, z)
{
  if (x == y) {
  } else {
    compose(x.next, y, z);
  }
}

lemma empty_list(x: Node)
  ensures lseg(x, x)
{
}

lemma fold_right(x: Node, y: Node)
  requires exists X: Set<Node> :: (lseg(x, y) &*& acc(y)) && acc(X) &*& y.next !in X
  ensures lseg(x, y.next)
{
  empty_list(y.next);
  compose(x, y, y.next);
  /* Alternatively:
  if (x == y) {
    assert acc(y) &*& lseg(y.next, z) &*& y != z;
  } else {
    append_right(x.next, y, z);
  }
  */
}

lemma fold_right_with_frame(x: Node, y: Node, z: Node)
  requires lseg(x, y) &*& y.next |-> z &*& lseg(z, null)
  ensures lseg(x, z) &*& lseg(z, null)
{
  fold_right(x, y);
}

procedure delete(x: Node)
  requires lseg(x, null)
  ensures emp
{
  if (x != null) {
    delete(x.next);
    free x;
  }
}

procedure traverse(x: Node)
  requires lseg(x, null)
  ensures lseg(x, null)
{
  assert lseg(x, null);
  var y := x;

  while (y != null)
    invariant lseg(x, y) &*& lseg(y, null)
  {
    unfold(y, null);
    fold_right(x, y);
    y := y.next;
  }
}

procedure test_frame(x: Node, y: Node, z1: Node, z2: Node)
  requires x.next |-> z1 &*& lseg(y, null)
  ensures x.next |-> z2 &*& lseg(y, null)
{
  x.next := z2;
}


procedure copy(lst: Node) returns (res: Node)
  requires lseg(lst, null)
  ensures lseg(lst, null) &*& lseg(res, null)
{
  var curr, cp: Node := lst, null;
  while (curr != null) 
    invariant lseg(lst, curr) &*& lseg(curr, null)
    invariant lseg(cp, null)
  {
    unfold(curr, null);
    fold_right(lst, curr);
    var old_cp := cp;
    cp := new Node;
    cp.next := old_cp;
    curr := curr.next;
  }
  return cp;
}

procedure reverse(lst: Node) 
  returns (rev: Node)
  requires lseg(lst, null)
  ensures lseg(rev, null)
{
  rev := null;
  var curr := lst;
  while (curr != null) 
    invariant lseg(rev, null)
    invariant lseg(curr, null)
  {
    var tmp := curr;
    curr := curr.next;
    tmp.next := rev;
    rev := tmp;
  }
}

procedure filter(x: Node) returns (res: Node)
  requires lseg(x, null)
  ensures lseg(res, null)
{
  var prv: Node, curr := null, x;
  res := x;
  while (curr != null) 
    invariant (prv.next |-> curr &*& lseg(res, prv) || prv == null &*& res == curr) &*& lseg(curr, null);
  {
    var old_curr := curr;
    curr := curr.next; 
    var nondet: Bool;
    if (nondet) {
      if (prv != null) {
        prv.next := old_curr.next;
      } else {
        res := old_curr.next;
      }
      free old_curr;
    } else {
      if (prv != null) {
        fold_right(res, prv);
      }
      prv := old_curr;
    }
  }

  if (prv != null) {
    fold_right(res, prv);
  }
}

