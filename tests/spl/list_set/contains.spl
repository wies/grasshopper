include "../include/slsset.spl";

procedure contains(lst: Node, val: Int, implicit ghost C: Set<Int>)
  returns (res: Bool)
  requires list_set(lst, null, C)
  ensures (res == val in C) &*& list_set(lst, null, C)
{
  var curr: Node;
  curr := lst;
  var prev: Node;
  prev := null;
  while (curr != null && curr.data < val) 
    invariant (prev == null && curr == lst || val > prev.data &*& prev.next |-> curr) -** 
      list_set(lst, null, C)
  {
    prev := curr;
    curr := curr.next;
  }
  if (curr != null && curr.data == val) return true;
  else return false;
}
